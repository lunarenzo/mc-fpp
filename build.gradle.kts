plugins {
    id("java")
    id("com.gradleup.shadow") version "9.4.1"
    id("io.papermc.paperweight.userdev") version "2.0.0-beta.21"
    id("xyz.jpenilla.run-paper") version "3.0.2"
    id("com.diffplug.spotless") version "7.0.3"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(25)
}

configurations.compileClasspath {
    attributes.attribute(TargetJvmVersion.TARGET_JVM_VERSION_ATTRIBUTE, 25)
}

tasks.compileJava {
    options.release.set(21)
    options.compilerArgs.add("-Xlint:deprecation")
}

group = "me.bill.fpp"

val baseVersion = "1.6.6.12.8"
val buildNumber = System.getenv("GITHUB_RUN_NUMBER")
val gitCommit = System.getenv("GIT_COMMIT_HASH")?.take(7)

version = when {
    buildNumber != null && gitCommit != null -> "$baseVersion-dev+b$buildNumber.$gitCommit"
    buildNumber != null -> "$baseVersion-dev+b$buildNumber"
    gitCommit != null -> "$baseVersion-dev+$gitCommit"
    else -> "$baseVersion-dev-local"
}

tasks.processResources {
    val props = mapOf("version" to project.version)
    inputs.properties(props)
    filteringCharset = "UTF-8"
    filesMatching("plugin.yml") {
        expand(props)
    }
}

repositories {
    mavenCentral()
    maven("https://maven.enginehub.org/repo/")
    maven("https://repo.extendedclip.com/content/repositories/placeholderapi/")
    maven("https://repo.papermc.io/repository/maven-public/")
    maven("https://repo.lucko.me/")
}

dependencies {
    paperweight.paperDevBundle("26.1.2.build.65-stable")

    compileOnly("net.luckperms:api:5.5")
    compileOnly("me.clip:placeholderapi:2.12.2")
    compileOnly("com.sk89q.worldguard:worldguard-bukkit:7.0.12") {
        exclude("com.google.code.gson", "gson")
        exclude("com.google.guava", "guava")
        exclude("it.unimi.dsi", "fastutil")
    }
    compileOnly("me.lucko:spark-api:0.1-SNAPSHOT")

    testImplementation("org.junit.jupiter:junit-jupiter:5.10.3")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

tasks.shadowJar {
    archiveBaseName.set("fake-player-plugin")
    manifest {
        attributes["Main-Class"] = "me.bill.fakePlayerPlugin.Launcher"
    }
}

tasks.test {
    useJUnitPlatform()
}

tasks.runServer {
    minecraftVersion("1.21.11")
    jvmArgs("-Xms1G", "-Xmx4G")
}

runPaper.folia.registerTask {
    minecraftVersion("1.21.11")
    jvmArgs("-Xms1G", "-Xmx4G")
}

spotless {
    java {
        target("src/**/*.java")
        palantirJavaFormat("2.56.0")
        importOrder("java", "javax", "org", "com", "me.bill")
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
    kotlinGradle {
        target("*.gradle.kts")
        ktlint()
    }
}
